apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.example.emotionrecognition"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    aaptOptions {
        noCompress "tflite"
    }
}

ext {
    versions = [
            androidxCore: "1.3.1",
            appCompat: "1.2.0",
            constraintLayout: "2.0.1",
            camerax: "1.0.0-beta07",
            cameraxView: "1.0.0-alpha14",
            splitties: "2.1.1",
            mlkit: "16.0.2",
            koin: "2.1.6",
            rxJava: "3.0.0",
            tensorflow: "2.3.0"
    ]
    androidxLibs = [
            core: "androidx.core:core-ktx:$versions.androidxCore",
            appCompat: "androidx.appcompat:appcompat:$versions.appCompat",
            constraintLayout: "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"
    ]
    cameraxLibs = [
            core: "androidx.camera:camera-camera2:$versions.camerax",
            lifecycle: "androidx.camera:camera-lifecycle:$versions.camerax",
            view: "androidx.camera:camera-view:$versions.cameraxView"
    ]
    splittiesLibs = [
            toast: "com.louiscad.splitties:splitties-toast:$versions.splitties"
    ]
    mlKitLibs = [
            faceDetection: "com.google.mlkit:face-detection:$versions.mlkit"
    ]
    koinLibs = [
            core: "org.koin:koin-android:$versions.koin",
            viewModel: "org.koin:koin-android-viewmodel:$versions.koin"
    ]
    rxJavaLibs = [
            core: "io.reactivex.rxjava3:rxjava:$versions.rxJava",
            rxAndroid: "io.reactivex.rxjava3:rxandroid:$versions.rxJava"
    ]
    tensorflowLibs = [
            core: "org.tensorflow:tensorflow-lite:$versions.tensorflow"
    ]
    externalLibs = androidxLibs.values() + cameraxLibs.values() + splittiesLibs.values() +
            mlKitLibs.values() + koinLibs.values() + rxJavaLibs.values() + tensorflowLibs.values()
}

repositories {
    maven { url "https://kotlin.bintray.com/kotlin-numpy" }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation externalLibs
    implementation project(path: ':openCVLibrary343')
    implementation 'org.tensorflow:tensorflow-lite-gpu:0.0.0-nightly'
    implementation 'org.tensorflow:tensorflow-lite-support:0.0.0-nightly'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}